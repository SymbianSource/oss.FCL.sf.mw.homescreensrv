#
# Copyright (c) 2009 Nokia Corporation and/or its subsidiary(-ies).
# All rights reserved.
# This component and the accompanying materials are made available
# under the terms of "Eclipse Public License v1.0"
# which accompanies this distribution, and is available
# at the URL "http://www.eclipse.org/legal/epl-v10.html".
#
# Initial Contributors:
# Nokia Corporation - initial contribution.
#
# Contributors:
#
# Description: 
#

TEMPLATE = lib
TARGET = afservice

CONFIG += plugin 
include(activityserviceplugin.pri)

CONFIG += qtservice 
QTSERVICE.DESCRIPTOR = data/afservice.xml

DEFINES += AFACTIVITIES_LIB

INCLUDEPATH += ./inc \
               ../inc \

HEADERS +=  ./inc/afserviceplugin.h \
            ./inc/afclient.h \
            ./inc/afmanager.h \
            ./inc/afactivitystorage_p.h \
            ./inc/afactivation_p.h \
            ./inc/afcommandlineparser.h \
            ./inc/afstorageproxy.h \

HEADERS +=  ../../homescreensrv_plat/activity_framework_api/afactivities_global.h \
            ../../homescreensrv_plat/activity_framework_api/afactivitystorage.h \
            ../../homescreensrv_plat/activity_framework_api/afactivation.h \

SOURCES +=  ./src/afserviceplugin.cpp \
            ./src/afclient.cpp \
            ./src/afmanager.cpp \
            ./src/aflauncher.cpp \
            ./src/afactivitystorage.cpp \
            ./src/afactivitystorage_p.cpp \
            ./src/afactivation.cpp \
            ./src/afactivation_p.cpp \
            ./src/afcommandlineparser.cpp \
            ./src/afstorageproxy.cpp \

symbian {
    INCLUDEPATH += ./s60/inc \

    SOURCES +=  ./s60/src/applicationlauncher_p.cpp \
                ./s60/src/afstorageproxy_p.cpp \
                ./s60/src/afserializer.cpp \
    
    HEADERS +=  ./s60/inc/applicationlauncher_p.h \
                ./s60/inc/afstorageproxy_p.h \
                ./s60/inc/afserializer.h \

    LIBS += -lapparc \
            -lapgrfx \
            -lcone \
            -lafstorageclient \
            -lxqutils \
            -lafstoragecommon.lib \
            -lestor \
            -lfbscli \

}

win32 {
    INCLUDEPATH += ./win/inc \

    SOURCES += ./win/src/applicationlauncher_p.cpp

    HEADERS += ./win/inc/applicationlauncher_p.h
}            
            
symbian {
    load(data_caging_paths) 
    TARGET.EPOCALLOWDLLDATA = 1
    TARGET.CAPABILITY = ALL -TCB
    TARGET.UID3 = 0x200267B2

    plugin.sources = afservice.dll
    plugin.path = $$QT_PLUGINS_BASE_DIR     
    
    xml.sources = ./data/afservice.xml
    xml.path = $$RESOURCE_FILES_DIR/activity
    
    DEPLOYMENT += xml plugin

    #temporary workaround
    BLD_INF_RULES.prj_exports += "data/afservice.xml z:/resource/activity/afservice.xml"
    
    #export xml with old name (remove after next release)
    BLD_INF_RULES.prj_exports += "data/afservice.xml z:/resource/activity/activityserviceplugin.xml"
    BLD_INF_RULES.prj_exports += "data/afservice.xml c:/resource/activity/activityserviceplugin.xml"
    
    # we want to export few classes, use standard .def block instead of the one generated by plugin
    defBlock = \
        "$${LITERAL_HASH}ifdef WINSCW" \
        "DEFFILE ./bwins/$${TARGET}.def" \
        "$${LITERAL_HASH}elif defined EABI" \
        "DEFFILE ./eabi/$${TARGET}.def" \
        "$${LITERAL_HASH}endif"
    MMP_RULES += defBlock
}


symbian:MMP_RULES += SMPSAFE
